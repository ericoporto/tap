// room script file
// Room2.asc
//
// This room tests the String struct from AGS Script API

function room_AfterFadeIn()
{
  tap.plan(23);
  
  // String.Append
  { 
    String mytext = "Hello";
    mytext = mytext.Append("World");
    tap.is(mytext, "HelloWorld", "String.Append test");
  }
  
  // String.AppendChar
  { 
    String mytext = "Hell";
    mytext = mytext.AppendChar('o');
    tap.is(mytext, "Hello", "String.AppendChar test");
  }
  
  // String.CompareTo
  {
    String mytext = "Hello";
    tap.ok(mytext.CompareTo("hello",eCaseInsensitive) == 0,  "String.CompareTo sensitivity off");
    tap.ok(mytext.CompareTo("hello",eCaseSensitive) != 0,  "String.CompareTo sensitivity on");
  }
  
  // String.Copy
  {
    String mystring = "This is a test string.";
    String newstring = mystring.Copy();
    tap.is(mystring, newstring, "String.Copy test");
  }
  
  // String.EndsWith
  {
    String myString = "Hello from the script!";
    tap.ok(myString.EndsWith("script!"), "String.EndsWith test");
  }
  
  // String.Format
  {
    int health=10;
    String mystring="test";
    String text = String.Format("%s , %d, %c", mystring, health, 'T');
    tap.is(text, "test , 10, T", "String.Format test");
    // NOTE: %f is locale dependant for comma or dot as separator.
  }
  
  // String.IndexOf
  {
    String haystack = "The haystack had a needle in it somewhere.";
    int result = haystack.IndexOf("a needle");
    tap.is_int(result, 17, "String.IndexOf test");
  }
  
  // String.IsNullOrEmpty
  {
    String null_string;
    String empty_string = "";
    String nonempty_string = "with text";
    tap.ok(String.IsNullOrEmpty(null_string), "String.IsNullOrEmpty test null string");
    tap.ok(String.IsNullOrEmpty(empty_string), "String.IsNullOrEmpty test empty string");
    tap.ok(!String.IsNullOrEmpty(nonempty_string), "String.IsNullOrEmpty test non-empty string");
  }
  
  // String.LowerCase
  {
    String mystring = "THIS is a test string";
    String lowercased = mystring.LowerCase();
    tap.is(lowercased, "this is a test string", "String.LowerCase test");
  }
  
  // String.Replace
  {
    String original = "Hello from the script!";
    String changed = original.Replace("hello", "goodbye");
    tap.is(changed, "goodbye from the script!", "String.Replace test");
  }
  
  // String.ReplaceCharAt
  {
    String mystring = "Hello";
    String changed = mystring.ReplaceCharAt(2, 'm');
    tap.is(changed, "Hemlo", "String.ReplaceCharAt test");    
  }
  
  // String.StartsWith
  {
    String myString = "Hello from the script!";
    tap.ok(myString.StartsWith("hello"), "String.StartsWith test");
  }
  
  // String.Substring
  {
    String mystring = "Hello World!";
    String substring = mystring.Substring(3, 5);
    tap.is(substring, "lo Wo", "String.Substring test");    
  }
  
  // String.Truncate
  {
    String mystring = "Hello World!";
    String truncated = mystring.Truncate(7);
    tap.is(truncated, "Hello W", "String.Truncate test");   
  }
  
  // String.UpperCase
  {
    String mystring = "THIS is a test string";
    String uppercased = mystring.UpperCase();
    tap.is(uppercased, "THIS IS A TEST STRING", "String.UpperCase test");  
  }
  
  // String.AsFloat
  {
    String text1, text2;
    float number1,number2;
    text1 = "57.362";
    text2 = "Hello";
    number1 = text1.AsFloat;
    number2 = text2.AsFloat;
    //tap.is_float(number1, 57.362, 0.0001, "String.AsFloat test with ''57.362''");
    tap.is_float(number2, 0.0, 0.0001, "String.AsFloat test with ''Hello''");
  }
  
  // String.AsInt
  {
    String text1, text2;
    int number1,number2;
    text1 = "53";
    text2 = "Hello";
    number1 = text1.AsInt;
    number2 = text2.AsInt;
    tap.is_int(number1, 53, "String.AsInt test with ''53''");
    tap.is_int(number2, 0, "String.AsInt test with ''Hello''");
  }
  
  // String.Chars
  {
    String text = "This is my string.";
    tap.is(String.Format("%c",text.Chars[3]), "s", "String.Chars test");
  }
  
  // String.Length
  {
    String text = "This is my string.";
    tap.is_int(text.Length, 18, "String.Length test");
    
  }

  tap.done_testing();
  QuitGame(0);
}

function room_Load()
{

}
